plugins {
    id 'application'
    id 'java-library'
}

java {
    //withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).all {
    options.compilerArgs += ['--enable-preview']
    options.release = 15
}
tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}
tasks.withType(Test) {
    jvmArgs += '--enable-preview'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.barassolutions.Nozc'
    }
}
javadoc.options {
    addBooleanOption('-enable-preview', true)
    addStringOption('-release', '15')
}

group 'com.barassolutions'
version = project.property("version")

processResources {
    filesMatching('**/app.properties') {
        filter {
            it.replace('%APP_VERSION_TOKEN%', version)
        }
    }
}

application {
    mainClass = 'com.barassolutions.Nozc'
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/junit/junit
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation group: 'org.jetbrains', name: 'annotations', version: '20.1.0'
}